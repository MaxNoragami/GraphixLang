<START> ::= <CODE_BLOCK>

<CODE_BLOCK> ::= <OPEN_BLOCK> <STATEMENTS> <CLOSE_BLOCK>

<STATEMENTS> ::= <STATEMENTS> <STATEMENT> | <STATEMENT>

<STATEMENT> ::= <DECLARATION_OP> |
				<ASSIGNMENT_OP> |
				<METADATA_OP> |
				<FILTER_OP> |
				<CROP_OP> |
				<ROTATE_OP> |
				<FOREACH_OP> |
				<CONDITIONAL_OP> |
                <ORIENTATION_OP> |
                <HUE_OP> |
                <WATERMARK_OP>


// From <STATEMENT>

<DECLARATION_OP> ::= <VAR_TYPE> <VAR_IDENTIFIER> <EOL> |
					<VAR_TYPE> <VAR_IDENTIFIER> <ASSIGN> <EXPRESSION> <EOL> |
                    <IMG_DECLARATION>

<IMG_DECLARATION> ::= <IMG_TYPE> <VAR_IDENTIFIER> <ASSIGN> <IMAGE_PATH> <EOL>

<ASSIGNMENT_OP> ::= <VAR_IDENTIFIER> <ASSIGN> <EXPRESSION> <EOL>

<BATCH_DECLARATION> ::= <BATCH_TYPE> <BATCH_IDENTIFIER> <ASSIGN> <BATCH_EXPRESSION> <EOL>

<IMAGE_PATH> ::= <STR_VALUE>

<METADATA_OP> ::= <VAR_IDENTIFIER> <ASSIGN> <METADATA> <VAR_IDENTIFIER> <FIELD> <EOL>

<FILTER_OP> ::= <SET> <VAR_IDENTIFIER> <FILER_TYPE> <EOL>

<HUE_OP> ::= <SET> <VAR_IDENTIFIER> <HUE> <INT_VALUE> <EOL>

<CROP_OP> ::= <CROP> <VAR_IDENTIFIER> <RESOULTION> <EOL>

<ORIENTATION_OP> ::= <ORIENTATION> <VAR_IDENTIFIER> <ORIENTATION_TYPE> <EOL>

<ROTATE_OP> ::= <ROTATE> <VAR_IDENTIFIER> <ROTATE_ACTION> <EOL>

<FOREACH_OP> ::= <FOREACH> <IMG_TYPE> <VAR_IDENTIFIER> <IN> <BATCH_IDENTIFIER> <CODE_BLOCK>

<CONDITIONAL_OP> ::=  <IF_BLOCK> <ELIF_PART> <ELSE_PART>

<WATERMARK_OP> ::= <TEXT_WATERMARK_OP> | <IMAGE_WATERMARK_OP>

<TEXT_WATERMARK_OP> ::= <WATERMARK> <VAR_IDENTIFIER> <STR_VALUE> <COLOR_VALUE> <EOL>

<IMAGE_WATERMARK_OP> ::= <WATERMARK> <VAR_IDENTIFIER> <VAR_IDENTIFIER> <INT_VALUE> <EOL>

// WITHIN OPERATIONS FROM <STATEMENT>

<FIELD> ::= <FWIDTH> |
			<FHEIGHT> |
			<FNAME> |
			<FSIZE>

<VAR_TYPE> ::= <TYPE_PXLS> |
				<TYPE_INT> |
				<TYPE_DBL> |
				<TYPE_BOOL> |
				<TYPE_STR>

<IMG_TYPE> ::= <TYPE_IMG>

<BATCH_TYPE> ::= <TYPE_BATCH>

<RESOLUTION> ::= <OPEN_P> <INT_VALUE> <COMMA> <INT_VALUE> <CLOSE_P>

<VAR_IDENTIFIER> ::= <VAR_SYMBOL><STRING>

<BATCH_IDENTIFIER> ::= <BATCH_SYMBOL><STRING>

<ROTATE_ACTION> ::= <LEFT> | <RIGHT>

<BATCH_EXPRESSION> ::= <BATCH_TERM> | <BATCH_EXPRESSION> <PLUS> <BATCH_TERM>

<BATCH_TERM> ::= <PATH> | <BATCH_IDENTIFIER>

<EXPRESSION> ::= <VAR_IDENTIFIER> | <BATCH_IDENTIFIER> | <PRIMITIVE_VALUE> | <EXPRESSION> <OPERATOR> <EXPRESSION>

<OPERATOR> ::= <PLUS> | <MINUS> | <MULTIPLY> | <DIVIDE>

<COLOR_VALUE> ::= <HEX_COLOR> | <RGB_COLOR>

<HEX_COLOR> ::= '~H' <HEX_DIGITS> '~'

<RGB_COLOR> ::= '~R' <RGB_DIGITS> '~'

<PLUS> ::= '+'

<MINUS> ::= '-'

<MULTIPLY> ::= '*'

<DIVIDE> ::= '/'

<PRIMITIVE_VALUE> ::= <INT_VALUE> |
						<DBL_VALUE> |
						<BOOL_VALUE> |
						<STR_VALUE> |
						<PXLS_VALUE>



// WITHIN THE <CONDITIONAL_OP>

<ELIF_PART> ::= <EMPTY> | <ELIF_BLOCK> <ELIF_PART>

<ELSE_PART> ::= <EMPTY> | <ELSE_BLOCK> <ELSE_PART>

<IF_BLOCK> ::= <IF> <CONDITION> <CODE_BLOCK>

<ELIF_BLOCK> ::= <ELIF> <CONDITION> <CODE_BLOCK>

<ELSE_BLOCK> ::= <ELSE> <CODE_BLOCK>

<CONDITION> ::= <EXPRESSION> <COMPARISON> <EXPRESSION> | <VAR_IDENTIFIER>

<COMPARISON> ::= <GREATER_EQUAL> |
				<SMALLER_EQUAL> |
				<GREATER> |
				<SMALLER> |
				<EQUAL> |
				<NOT_EQUAL>


// WITHIN THE OPERATIONS OF OPERATIONS FROM <STATEMENT>

<FILER_TYPE> ::= <SEPIA> |
				<BW> |
				<NEGATIVE> |
				<SHARPEN>
				
<ORIENTATION_TYPE> ::= <LANDSCAPE> |
                       <PORTRAIT>


// OTHERS

<SEPIA> ::= 'SEPIA'

<BW> ::= 'BW'

<NEGATIVE> ::= 'NEGATIVE'

<SHARPEN> ::= 'SHARPEN'

<OPEN_BLOCK> ::= '{'

<CLOSE_BLOCK> ::= '}'

<ASSIGN> ::= '='

<IF> ::= 'IF'

<ELSE> ::= 'ELSE'

<ELIF> ::= 'ELIF'

<VAR_SYMBOL> ::= '$'

<BATCH_SYMBOL> ::= '#'

<EOL> ::= ';'

<HUE> ::= 'HUE'

<EMPTY> ::= ''

<TYPE_PXLS> ::= 'PIXEL'

<TYPE_INT> ::= 'INT'

<TYPE_DBL> ::= 'DOUBLE'

<TYPE_BOOL> ::= 'BOOL'

<TYPE_STR> ::= 'STRING'

<TYPE_IMG> ::= 'IMG'

<TYPE_BATCH> ::= 'BATCH'

<INT_VALUE> ::= [0-9]+

<DBL_VALUE> ::= [0-9.]+

<BOOL_VALUE> ::= 'TRUE' | 'FALSE'

<STR_VALUE> ::= "\"" [A-Za-z0-9./]+ "\""

<HEX_DIGITS> ::= [0-9A-Fa-f]{8}

<RGB_DIGITS> ::= [0-9]{12}

<GREATER_EQUAL> ::= '>='

<SMALLER_EQUAL> ::= '<='

<GREATER> ::= '>'

<SMALLER> ::= '<'

<EQUAL> ::= '=='

<NOT_EQUAL> ::= '!='

<SET> ::= 'SET'

<COMMA> ::= ','

<OPEN_P> ::= '('

<CLOSE_P> ::= ')'

<LEFT> ::= 'LEFT'

<RIGHT> ::= 'RIGHT'

<PATH> ::= <STR_VALUE>

<PXLS_VALUE> ::= [0-9]+p

<METADATA> ::= 'METADATA'

<FWIDTH> ::= 'FWIDTH'

<FHEIGHT> ::= 'FHEIGHT'

<FNAME> ::= 'FNAME'

<FSIZE> ::= 'FSIZE'

<FOREACH> ::= 'FOREACH'

<CROP> ::= 'CROP'

<ORIENTATION> ::= 'ORIENTATION'

<WATERMARK> ::= 'WATERMARK'

<ROTATE> ::= 'ROTATE'

<IN> ::= 'IN'